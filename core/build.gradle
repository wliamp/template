import static java.lang.System.getenv
import static org.gradle.api.JavaVersion.VERSION_17
import static org.gradle.jvm.toolchain.JavaLanguageVersion.of

plugins {
    id 'java'
    id 'org.springframework.boot' version '3.5.7'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'jacoco'
}

java {
    toolchain {
        languageVersion = of(25)
    }
    targetCompatibility = VERSION_17
    sourceCompatibility = VERSION_17
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
    maven {
        url = uri("https://maven.pkg.github.com/hamsaqua/starter")
        credentials {
            username = getenv("GITHUB_ACTOR") ?: findProperty("github.actor")
            password = getenv("GITHUB_TOKEN") ?: findProperty("<alias1>.<alias2>.core.token")
        }
    }
}

dependencies {
    implementation libs.starter
    implementation 'io.projectreactor:reactor-core'
    compileOnly 'org.springframework.data:spring-data-commons'
    compileOnly 'org.springframework:spring-context'
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation 'io.projectreactor:reactor-test'
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testImplementation 'org.mockito:mockito-core'
    testImplementation 'org.springframework:spring-test'
    testImplementation 'org.testcontainers:junit-jupiter'
    testImplementation 'org.testcontainers:postgresql'
}

tasks.withType(Test).configureEach {
    useJUnitPlatform()
    finalizedBy(tasks.jacocoTestReport)
    javaLauncher.set javaToolchains.launcherFor {
        languageVersion.set(of(17))
    }
}


tasks.named('jacocoTestReport', JacocoReport) {
    dependsOn tasks.test
    reports {
        xml.required.set(true)
        html.required.set(true)
    }
}
