import static java.lang.System.getenv
import static org.gradle.api.JavaVersion.VERSION_17
import static org.gradle.jvm.toolchain.JavaLanguageVersion.of

plugins {
    id "java"
    id "org.springframework.boot" version "3.5.7"
    id "io.spring.dependency-management" version "1.1.7"
}

def actor = getenv("ACTOR") ?: findProperty("actor")
def org = getenv("ORG") ?: findProperty("org.artifact")
def repo = getenv("REPO") ?: findProperty("repo.artifact")
def token = getenv("TOKEN") ?: findProperty("repo.artifact.pat")

java {
    toolchain {
        languageVersion = of(25)
    }
    targetCompatibility = VERSION_17
    sourceCompatibility = VERSION_17
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
    maven {
        url = uri("https://maven.pkg.github.com/${org}/${repo}")
        credentials {
            username = actor
            password = token
        }
    }
}

dependencies {
    implementation libs.artifact
    implementation "io.projectreactor:reactor-core"
    compileOnly "org.springframework.data:spring-data-commons"
    compileOnly "org.springframework:spring-context"
    compileOnly "org.projectlombok:lombok"
    annotationProcessor "org.projectlombok:lombok"
    testImplementation "io.projectreactor:reactor-test"
    testImplementation "org.junit.jupiter:junit-jupiter"
    testImplementation "org.mockito:mockito-core"
    testImplementation "org.springframework:spring-test"
    testImplementation "org.testcontainers:junit-jupiter"
    testImplementation "org.testcontainers:postgresql"
}
